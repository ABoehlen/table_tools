'########################################################
'#
'# Filename:     TableTools_AddRecords.ave
'# Author:       Adrian Boehlen
'# Date:         29.05.2014
'# Version:      0.7
'#
'# Purpose:      fuegt die gewuenschte Anzahl Records
'#               der Tabelle hinzu
'#
'########################################################

'# die Tabelle muss aktiv und im Edit-Modus sein
theTable = av.GetActiveDoc

if (theTable.Is(table).Not) then
  MsgBox.Warning("Start this script from an active Table.","")
  return nil
end

theVTab = theTable.GetVTab

if (theVTab.CanEdit.Not) then
  MsgBox.Warning("Die Tabelle kann nicht bearbeitet werden","Abbruch")
  return nil
end

if (theVTab.IsEditable.Not) then
  theVTab.StartEditingWithRecovery
  theEditstatus = 0
else
  theEditstatus = 1
end

'# zu ergaenzende Zeilen definieren und pruefen
theRecords = MsgBox.Input("Geben Sie die Anzahl zu ergaenzender Zeilen ein", "Hinzuzufuegende Zeilen definieren",1.AsString)

if (theRecords = nil) then
  return nil
elseif (theRecords.IsNumber.Not) then
  MsgBox.Warning("Ungueltige Eingabe","Abbruch")
  return nil
elseif (theRecords.AsNumber < 1) then
  MsgBox.Warning("Anzahl Zeilen ist zu gering","Abbruch")
  return nil
end

'# neue Zeilen ergaenzen
for each rec in 1..theRecords.AsNumber
  theTable.GetVTab.BeginTransaction
  theTable.GetVTab.AddRecord
  theTable.GetVtab.EndTransaction
end

'# Editsession beenden, wenn zu Beginn nicht gestartet
if (theEditstatus = 0) then
  theVTab.SetEditable(false)
end
